import Preview from '../../components/preview'
import { Callout } from 'nextra-theme-docs'

# Using Playroom with Godot 4 (Web only)

[Godot](https://godotengine.org/) is a free and open-source game engine. Godot has [JavaScriptBridge](https://docs.godotengine.org/en/stable/classes/class_javascriptbridge.html) support, which connects the engine with the browser's JavaScript. We can use this to use Playroom with Godot.

<Callout emoji="ðŸ‘·ðŸ¼â€â™€ï¸">
  Godot 4's web export is not production-grade yet. There are open bugs in [Chromium](https://bugs.chromium.org/p/chromium/issues/detail?id=1324296#c6) and in [Godot](https://github.com/godotengine/godot/issues/83548) [itself](https://github.com/godotengine/godot/issues/70691).
</Callout>

## Setup

- In your Godot project, you need to enable Web Export. You can do this by going to `Project > Export > Add` and clicking `Web`.

- With `Web` selected, add Playroom to the `HTML > Head` section:

```html
<script src="https://unpkg.com/react@18.2.0/umd/react.development.js" crossorigin="anonymous"></script>
<script src="https://unpkg.com/react-dom@18.2.0/umd/react-dom.development.js" crossorigin="anonymous"></script>
<script src="https://unpkg.com/playroomkit/multiplayer.umd.js" crossorigin="anonymous"></script>
```

You can close the export window now.

- If you completed the above steps correctly, you should see a `Run in browser` menu option in when you click the `Remote Debug` button:

![Run in browser](/images/godot-runinbrowser.png)


## Using Playroom in GDScript

To initialize Playroom in your game ie. show the Playroom lobby, you need to call `Playroom.insertCoin()` in some script. `insertCoin` and other Playroom functions also support providing a callback function. You need to make your GDScript callback function available to the JS context. 

Here is an example of how to initialize Playroom on a button press:

```gdscript
extends Button

var Playroom = JavaScriptBridge.get_interface("Playroom")

# Keep a reference to the callback so it doesn't get garbage collected
var jsBridgeReferences = []
func bridgeToJS(cb):
	var jsCallback = JavaScriptBridge.create_callback(cb)
	jsBridgeReferences.push_back(jsCallback)
	return jsCallback

# Called when the "Play Multiplayer" button is pressed.
func _on_pressed():
	var initOptions = JavaScriptBridge.create_object("Object");
	Playroom.insertCoin(initOptions, bridgeToJS(onInsertCoin));

# Called when the host has started the game
func onInsertCoin(args):
	print("Coin Inserted")
	Playroom.onPlayerJoin(bridgeToJS(onPlayerJoin))

# Called when a new player joins the game
func onPlayerJoin(args):
	var state = args[0];
	print("new player: ", state.id)
	print(state.getProfile().name)
	
	# Listen to onQuit event
	var onQuitCb = func onPlayerQuit(args):
		print("player quit: ", state.id)
	state.onQuit(bridgeToJS(onQuitCb))

```

The above code will show the familiar [Playroom lobby](components/lobby) when the button is pressed. Players can host and join rooms without any code on your side. 

When the host starts the game, the `onInsertCoin` callback will be called. The `onPlayerJoin` callback will be called for all existing and new players, so you can add characters for them. When a player quits the game, the `onPlayerQuit` callback will be called, so you can remove their character.

<Callout emoji="âš ï¸">
  Do note that Playroom will not work in the Godot editor. You need to export your game and run it in the browser.
</Callout>

<Callout type="info" emoji="ðŸ’¡">
  You can use the `Run in browser` option in the `Remote Debug` menu to quickly build and open your game in the browser.
</Callout>

<Callout type="info" emoji="ðŸ’¡">
  If you host the game on your own server, Godot requires the following headers to be present, add these in your `.htaccess` or however your server lets you:

```
Cross-Origin-Opener-Policy: same-origin
Cross-Origin-Embedder-Policy: require-corp
```

</Callout>

## Simple Demo

<Preview src="/demos/godot1/index.html" newWindow />

See the full [source code](https://github.com/asadm/playroom-docs/tree/main/examples/godot-hello-world) for this demo.


## Next Steps

- You should be able to use most of [Playroom API](/docs/api.md) to build your game.
- You can use [On-screen Joystick](/components/joystick) to easily add a joystick on screen.

